plugins {
    id 'java'
    id 'com.diffplug.spotless' version '6.13.0'
    id 'com.gradleup.shadow' version '9.0.0-beta13'
}

group = 'me.zypj.rollerite'
version = '1.0'

repositories {
    mavenCentral()
    maven { url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
}

dependencies {
    compileOnly (
            'org.spigotmc:spigot-common:1.21.5-R0.1-SNAPSHOT',
            'org.projectlombok:lombok:1.18.38'
    )
    implementation project(':essentials-common')


    annotationProcessor('org.projectlombok:lombok:1.18.38')
}

def targetJavaVersion = 21
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

shadowJar {
    archiveFileName.set("Essentials-${project.version}.jar")

    relocate 'me.zypj.essentials.common', 'me.zypj.essentials.common'
    relocate 'me.zypj.essentials', 'me.zypj.essentials'

    subprojects.each { sp ->
        def ss = sp.extensions.findByType(SourceSetContainer)
        if (ss != null) {
            from(ss.main.output)
        }
    }

    mergeServiceFiles()
}

spotless {
    java {
        target 'src/**/*.java'
        googleJavaFormat('1.27.0')
        trimTrailingWhitespace()
        endWithNewline()
    }
}